import "common.cgl"

baseColor = rgb(245, 245, 245)
mainColor = rgb(49, 187, 222)

fontPath = "C:/Users/dphs1/AppData/Roaming/Adobe/CoreSync/plugins/livetype/r/35668"
sourceFontPath = "C:/Windows/Fonts/SourceCodePro-Regular.ttf"

sourceCodeText =
"{
    size = 100
    polygon: [
        {x: 0, y: 0}, {x: size, y: 0}
        {x: size, y: size}, {x: 0, y: size}
    ]
    fill: {r: 49, g: 187, b: 222}
}"

sourceCodeShape = {
    a: BuildText(sourceCodeText, {}, sourceFontPath)
}

main = {
    a: square{scale: x2(300), angle: 45, fill: mainColor, 
        basePos: (->lerp(topLeft(), bottomRight(), 0.3))}
    b: a{pos: a.topLeft(), fill: mainColor
        sat( distant(a.top(), bottom(), 10) & parallel([a.bottom(), a.top()], [a.top(), bottom()]) ), 
        var(pos)}
    c: a{pos: a.topRight(), fill: mainColor
        sat( distant(a.right(), left(), 10) & parallel([a.left(), a.right()], [a.right(), left()]) ), 
        var(pos)}
    d: shape{
        text: BuildText("構文", {}, fontPath)
        fill: baseColor
        pos: b.bottomRight()
    }
    e: shape{
        text: BuildText("図形演算", {}, fontPath)
        fill: baseColor
        pos: a.bottomRight()
    }
    f: shape{
        text: BuildText("制約処理", {}, fontPath)
        fill: baseColor
        pos: c.center()
    }
    aOut: shape{
        line: [a.topLeft(), a.topRight()]
    }
    bOut: shape{
        line: [b.topLeft(), b.topRight()]
    }
    cOut: shape{
        line: [c.topLeft(), c.topRight()]
    }

    g: shape{
        s: Intersect(Buffer(sourceCodeShape{
            bb = BoundingBox(b)
            tl = bb.topLeft
            br = bb.bottomRight
            pos: vec2(
                tl.x + (br.x - tl.x)*0.3,
                tl.y + (br.y - tl.y)*0.35)
            scale: x2(0.4)
        }, 0.5), b)
        fill: baseColor
    }
    h: shape{
        bens: shape{
            left= circle(60){scale: x2(120)}
            right= left{pos: vec2(80, 0)}
            s: SymDiff(left, right)
            pos: lerp(a.topLeft(), a.bottomRight(), 0.6)
        }
        sat(BoundingBox(bens).center.x == a.topLeft().x)
        var(bens.pos.x)
        fill: baseColor
    }
    i: shape{
        s: shape{
            ss: []
            for i in -3:3 do(
                index = 3+i
                ss[index] = square{
                    scale: x2(30)
                    pos: {x: 35*i, y: 0}
                }
                sat( contact(ss[index].topLeft(), vec2(ss[index].topLeft().x, 40*Sin(-2*ss[index].topLeft().x))) )
                sat( contact(ss[index].bottomRight(), vec2(ss[index].bottomRight().x, 40*Sin(-2*ss[index].bottomRight().x))) )
            )
            for i in -3:2 do(
                index = 3+i
                sat( contact(ss[index].bottomRight(), ss[index + 1].topLeft()) )
            )
            pos: lerp(c.topLeft(), c.bottomRight(), 0.6)
            var(ss[*].pos, ss[*].angle)
        }
        sat(BoundingBox(s).center.x == c.topLeft().x)
        var(s.pos.x)
        fill: baseColor
    }


    sat( b.topLeft().x == BoundingBox(d).center.x )
    sat( Distance(bOut, d) == 20.0 )

    sat( a.topLeft().x == BoundingBox(e).center.x )
    sat( Distance(aOut, e) == 20.0 )

    sat( Cmaes(f.pos), c.topLeft().x == BoundingBox(f).center.x )
    sat( Distance(cOut, f) == 20.0 )
    var(d.pos, e.pos, f.pos)
}


main2 = shape{
    m: main
    l: {
        stroke: baseColor
        fill: baseColor
        ls1= [
            ClosestPoints(m.cOut, m.f)
            ClosestPoints({line: [m.c.topLeft(), m.c.bottomLeft()]}, m.f)

            ClosestPoints(m.aOut, m.e)
            ClosestPoints({line: [m.a.topLeft(), m.a.bottomLeft()]}, m.e)

            ClosestPoints(m.bOut, m.d)
            ClosestPoints({line: [m.b.topLeft(), m.b.bottomLeft()]}, m.d)
        ]
        ls: for i in 0:5 list(
            currentLine = ls1[i].line
            {
                line: [currentLine[0], lerp(currentLine[0], currentLine[1], 0.75)]
                s: circle(10){scale: x2(2.0), pos: line[1]}
            }
        )
    }
}
